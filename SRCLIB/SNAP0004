SNAP0004 TITLE ' '
SNAP0004 CSECT
SNAP0004 AMODE 31
SNAP0004 RMODE ANY
         PRINT NOGEN
@PROLOG  $GJNLINK PROLOG,PARMPTR=10     -std ALC entry
*
* Load Interface Routine
*
         LOAD  EP=IRXEXCOM              -load address of interface
         LR    R8,R0                    -save address for calls
*
* Obtain Storage for a this program
*
         STORAGE OBTAIN,               -obtain storage                 +
               LENGTH=DYNLENTH
*
         ST    R1,IRPARMS+4*3          -store in parmlist
         OI    IRPARMS+4*3,X'80'       -set for last parm
         LR    R9,R1                   -save storage address
         USING SHVBLOCK,R9             -map REXX area
         LR    R0,R9                   -load address
         L     R1,=A(DYNLENTH)         -load length
         XR    R14,R14                 -zero sender
         XR    R15,R15                 -zero sender length
         ICM   R15,B'1000',=X'00'      -set pad character
         MVCL  R0,R14                  -zero workarea
*
* Set arguments for all calls
*
         MVI   SHVCODE,SHVNEXTV        -set indicator for NEXT
         MVC   SHVUSER,=A(250)         -set length for V-Name
         MVC   SHVBUFL,=A(100000)      -set length for V-Value
         LA    R1,SHVBLEN(R9)          -get address for V-Name
         ST    R1,SHVNAMA              -set address
         LA    R1,SHVBLEN+250(R9)      -get address for V-Value
         ST    R1,SHVVALA              -set address
         XC    COUNT,COUNT
@LOOP000 EQU   *
         LR    R15,R8
         CALL  (15),MF=(E,IRPARMS)
         TM    SHVRET,SHVLVAR
         BO    @FREE
         $CONVERT C2D,                                                 +
               I=SHVNAML,                                              +
               O=OUT70NLE,                                             +
               MODE=
         $CONVERT C2D,                                                 +
               I=SHVVALL,                                              +
               O=OUT70VLE,                                             +
               MODE=
         L     R1,COUNT
         LA    R1,1(R1)
         ST    R1,COUNT
         $CONVERT C2D,                                                 +
               I=COUNT,                                                +
               O=OUT70CNT,                                             +
               MODE=
         $RPT  R=O70S                   -write record
         LA    R1,SHVNAMA
         CALL  SNAP0000
         LA    R1,SHVVALA
         CALL  SNAP0000
         B     @LOOP000
*
@FREE    EQU   *
         STORAGE RELEASE,                                              +
               LENGTH=DYNLENTH,                                        +
               ADDR=(9)
*
@EPILOG  $GJNLINK EPILOG
*
DYNLENTH EQU   1024*1024
IRPARMS  CALL  ,(ID,,,),MF=L,VL
ID       DC    CL8'IRXEXCOM'
*
COUNT    DS    F
*
O70S     DS    CL(O70Z)
         ORG   O70S
O70A     EQU   *
         DC    CL1'0'
         DC    C'New Variable: '
         DC    C'Accumulative('
OUT70CNT DC    CL6' '
         DC    C') Name Length('
OUT70NLE DC    CL6' '
         DC    C') Value Length('
OUT70VLE DC    CL6' '
         DC    C')'
O70Z     EQU   *-O70A
*
         $REXX INCLUDE
         END   SNAP0004
