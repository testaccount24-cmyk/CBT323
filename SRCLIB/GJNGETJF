GJNGETJF TITLE 'Return a JFCB for a given allocation/concatenation'
GJNGETJF CSECT
GJNGETJF AMODE 31
GJNGETJF RMODE ANY
         PRINT NOGEN
@PROLOG  $GJNLINK PROLOG,PARMPTR=R10    -entry with EFPL in R10
         LM    R6,R9,0(R10)
         LA    R1,ANYFILE
         USING IHADCB,R1
         MVC   DCBDDNAM,0(R6)
         XR    R1,R1                    -zero register
         IC    R1,0(R7)                 -load start point
         STCM  R1,B'0011',ARLFIRST      -set start point
         XR    R0,R0                    -zero register for divide
         L     R1,0(R8)                 -load length of area
         D     R0,=F'180'               -divide by length of JFCB
         STCM  R1,B'0011',ARLRETRV      -retreive this many
         RDJFCB (ANYFILE)               -get allocation info
         XR    R5,R5
         ICM   R5,B'0011',ARLCONC
         ICM   R5,B'1100',ARLRTRVD
         LTR   R15,R15                  -test return code
         BNZ   @EPILOG                  -if not zero, then exit
         LR    R0,R9
         L     R1,0(R8)
         L     R14,ARLAREA
         ICM   R15,B'0111',ARLRLEN
         ICM   R15,B'1000',=C' '
         MVCL  R0,R14
         XR    R2,R2
         IC    R2,ARLPOOL
         XR    R3,R3
         ICM   R3,B'0111',ARLRLEN
         STORAGE RELEASE,                                              +
               LENGTH=(3),                                             +
               SP=(2),                                                 +
               ADDR=ARLAREA
@EPILOG  EQU   *
         ST    R5,RC
         $GJNLINK EPILOG,LASTCC=RC      -exit with RC
ANYFILE  DCB   DSORG=PO,                                               +
               MACRF=R,                                                +
               EXLST=INEXLST
RC       DC    F'0'
INEXLST  DC    0F'0'                    -entry code for open exit
         DC    X'13'                    -retreive allocation info
         DC    AL3(SLBSTRT)             -allocation retreival list
         DC    X'80'
         DC    AL3(0)
SLBSTRT  IHAARL DSECT=NO
         DCBD  DSORG=PO
         END   GJNGETJF
