SLEEP    TITLE 'REXX function- Sleep for specified time'
*
**********************************************************************
*                                                                    *
* Component_Type: MVS HL-Assembler REXX Function                     *
*    Description: Place REXX program in sleep mode for passed time   *
*         Author: Gerard J Nicol                                     *
*           Date: September 1997                                     *
*                                                                    *
*--+----1----+----2----+----3----+----4----+----5----+----6----+----7*  --+----8
*
SLEEP    CSECT
SLEEP    AMODE 31
SLEEP    RMODE 24
         PRINT GEN
@PROLOG  $GJNLINK PROLOG,PARMPTR=10     -std entry (link stack)
         $REXX ENTRY,ARGS=(1,0)         -std REXX entry
         $REXX VER,                                                    +
               A=1,                                                    +
               VTYPE=LENGTH,                                           +
               PARMS=(8,8),                                            +
               NB=Y
         $REXX VER,                                                    +
               A=1,                                                    +
               VTYPE=DATATYP,                                          +
               PARMS=(NUM),                                            +
               NB=Y
         L     R2,0(R11)                -load address of parm-1
         MVI   RESULT,C'1'              -set default result
         STAX  @EXPIRE                  -setup attention exit
         STIMER REAL,                                                  +
               @EXPIRE,                                                +
               DINTVL=0(R2)
         WAIT  1,ECB=ECB
@RESULT  EQU   *
         $REXX RESULT,V=RESULT          -set result
@EPILOG  $GJNLINK EPILOG,LASTCC=RC      -exit program
*
RC       DC    XL1'00'
*
         DS    0F
RESULT   DC    CL1'0'
ECB      DC    A(0)                     -ECB must be -4 from @EXPIRE
*
@EXPIRE  EQU   *
         DROP  R12
         LR    R10,R14                  -save return address
         LR    R12,R15                  -save base address
         USING @EXPIRE,R12              -map sub-program
         LR    R3,R12
*
         LA    R0,8
         SR    R3,R0
*
         LTR   R1,R1
         BZ    @NOATTN
         MVI   0(R3),C'0'
@NOATTN  EQU   *
         LA    R3,4(R3)
         POST  (3),0                    -post ECB as complete
         BR    R10                      -return to caller
*
         $REXX INCLUDE
*
         END   SLEEP
