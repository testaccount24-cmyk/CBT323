GJNCNCAT TITLE 'Concatenate an array of passed DDS'
GJNCNCAT CSECT
GJNCNCAT AMODE ANY
GJNCNCAT RMODE 24
         PRINT NOGEN
@PROLOG  $GJNLINK PROLOG
*
         L     R1,0(R1)                 -load address of only parm
         LH    R2,0(R1)                 -load length of parms
         LA    R3,2(R1)                 -load address of first parm
         LR    R4,R2                    -reload length
         SRL   R4,2                     -divide by 4
         LA    R9,4(R2,R4)              -add 4 + 8*total +2*total
         STORAGE OBTAIN,LENGTH=(9),LOC=BELOW
         LR    R10,R1                   -save address
         MVC   0(2,R10),=AL2(DCCDDNAM)  -set text key
         SRL   R2,3                     -divide total by 8
         STH   R2,2(R10)                -set total key values
         LA    R5,4(R10)                -load starting point
@DDLOOP  EQU   *
         MVC   0(2,R5),=AL2(8)          -set length of DDName
         MVC   2(8,R5),0(R3)            -set DDName
         LA    R5,10(R5)                -move to next key value
         LA    R3,8(R3)                 -move to next DDName
         BCT   R2,@DDLOOP
         LA    R2,RB                    -get address of workarea
         LA    R3,RBX                   -get address of extension
         USING S99RB,R2                 -map to request block
         USING S99RBX,R3
*
         XC    RBX,RBX
         MVC   S99EID,=CL6'S99RBX'      -setup eyecatcher
         MVI   S99EVER,S99RBXVR
         MVI   S99EOPTS,S99ELSTO
         MVI   S99EMGSV,S99XINFO
*
         XC    S99RB,S99RB              -set request block to X'00'
         ST    R3,S99S99X               -store address of extension
         MVI   S99RBLN,RBLEN            -set request block length
         MVI   S99VERB,S99VRBCC         -set concatenation function
         ST    R10,$CONCAT              -store address of text
         OI    $CONCAT,X'80'            -set HO bit ON
         LA    R1,$CONCAT               -load address of text list
         ST    R1,S99TXTPP              -set pointer to text area
         LA    R1,$DYNPTR               -sets address of RB ptr
         DYNALLOC
         STC   R15,RC
*
         STORAGE RELEASE,                                              +
               LENGTH=(9),                                             +
               ADDR=(10)
*
@EPILOG  $GJNLINK EPILOG,LASTCC=RC      -exit with RC
*
RC       DC    XL1'00'
$DYNPTR  CALL  ,(RB),VL,MF=L
$CONCAT  CALL  ,(DD#TU),VL,MF=L
RBLEN    EQU   S99RBEND-S99RB
RBXLEN   EQU   (S99ERSN-S99RBX)+4
RB       DS    CL(RBLEN)
RBX      DS    CL(RBXLEN)
*
DD#TU    DC    AL2(DCCDDNAM)
         DC    X'0002'
         DC    X'0008'
DDNAME01 DC    CL8' '
         DC    X'0008'
DDNAME02 DC    CL8' '
*
         IEFZB4D0
         IEFZB4D2
*
         END   GJNCNCAT
