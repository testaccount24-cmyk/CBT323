         MACRO
.*
.**********************************************************************
.*                                                                    *
.* Component_Type: MVS HL-Assembler Macro                             *
.*    Description:                                                    *
.*         Author: Gerard J Nicol                                     *
.*           Date: September 1997                                     *
.*                                                                    *
.*--+----1----+----2----+----3----+----4----+----5----+----6----+----7* --+----8
.*
&LABEL   $PERF &MODE,                   -GO, BACK                      +
               &A=,                     -address for branch            +
               &L=                      -nesting level
.*
         GBLC  &PA
&LE      SETA  K'&PA
&FO      SETB  0
.*
.GOSCAN  AIF   ('&LE' EQ '0').ENDSCAN
         AIF   ('&PA'(&LE,&LE) EQ '&L').FOUND
&LE      SETA  &LE-1
         AGO   .GOSCAN
.FOUND   ANOP
&FO      SETB  1
         MNOTE *,'Level already defined, Storage will be re-used'
.ENDSCAN ANOP
.*
         AIF ('&MODE' EQ 'GO').GO
         AIF ('&MODE' EQ 'BACK').BACK
         MNOTE 12,'Invalid mode spacified: &MODE'
         MEXIT
.*
.GO      ANOP
         LA    R1,Z&SYSNDX              -load address of return point
         ST    R1,GJNPRF&L              -store address for BACK
         B     &A                       -branch to routine
         AIF   ('&FO' EQ '1').GOSKIP
GJNPRF&L DS    A                        -branch to routine
&PA      SETC  '&PA'.'&L'
.GOSKIP  ANOP
Z&SYSNDX DS    0H
         AGO   .END
.*
.BACK    ANOP
         L     R1,GJNPRF&L              -load address of caller
         BR    R1                       -branch back
         AGO   .END
.*
.END     ANOP
         MEND
