         MACRO
.*
.**********************************************************************
.*                                                                    *
.* Component_Type: MVS HL-Assembler Macro                             *
.*    Description: Reporting Macro                                    *
.*         Author: Gerard J Nicol                                     *
.*           Date: September 1997                                     *
.*                                                                    *
.*--+----1----+----2----+----3----+----4----+----5----+----6----+----7* --+----8
.*
&LABEL   $RPT  &DCB=,                   -address of DCB for write      +
               &INIT=N,                 -code Y on first call          +
               &FBREAK=N,               -force page break              +
               &RPP=40,                 -records per page value        +
               &R=,                     -address of record for write   +
               &RL=,                    -length of record for write    +
               &H=,                     -address of header for write   +
               &HL=,                    -length of header for write    +
               &USERA=0
.*
         $DEFL V=&R,VL=&RL,S=A&SYSNDX   -establish default values
         $DEFL V=&H,VL=&HL,S=B&SYSNDX   -establish default values
.*
         L     R1,=V($GJNWRIT)          -load address of arg area
         AIF   ('&INIT' NE 'Y').NOINIT  -was INIT coded
         LA    R15,&DCB                 -load address of DCB
         ST    R15,0(R1)                -store in common area
         XC    4(4,R1),4(R1)            -zero line count
         MVC   8(4,R1),=A(&RPP)         -set records/page
         MVC   20(4,R1),=A(&H)          -set header address
         MVC   24(4,R1),=A(B&SYSNDX)    -set header length
         MVC   28(4,R1),=A(&USERA)      -set user area address
         AGO   .END9999
.NOINIT  ANOP
         LA    R14,&R                   -load address of record
         LA    R15,A&SYSNDX             -load length of record
         STM   R14,R15,12(R1)           -set address/length
         AIF   ('&FBREAK' NE 'Y').NB    -do we want to force a break
         OI    12(R1),X'80'             -set mode flag
.NB      ANOP
         L     R15,=V(GJNWRITE)         -load address of processor
         BALR  R14,R15                  -branch to processor
.END9999 ANOP
         MEND
